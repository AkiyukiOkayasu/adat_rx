# ADAT Receiver Verilator justfile
#
# 使用方法:
#   just           - ビルドと実行(トレースなし)
#   just run       - ビルドと実行(トレースなし)
#   just run-trace - ビルドと実行（FSTトレース出力付き）
#   just wave      - 波形表示 (Surfer)
#   just clean     - クリーンアップ

PROJ_ROOT := "../.."
OBJ_DIR := "obj_dir"

VERILATOR := "verilator"
VERILATOR_FLAGS := "--cc --exe --build -j 0 -Wall -Wno-UNUSEDSIGNAL -Wno-UNUSEDPARAM -Wno-TIMESCALEMOD -Wno-WIDTHEXPAND -Wno-WIDTHTRUNC -Wno-PINCONNECTEMPTY --timing"
VERILATOR_TRACE_FLAGS := "--cc --exe --build -j 0 -Wall -Wno-UNUSEDSIGNAL -Wno-UNUSEDPARAM -Wno-TIMESCALEMOD -Wno-WIDTHEXPAND -Wno-WIDTHTRUNC -Wno-PINCONNECTEMPTY --timing --trace-fst"

default: run

# Verylフォーマット
fmt:
  cd {{PROJ_ROOT}} && veryl fmt

# Verylビルド
veryl: fmt
  cd {{PROJ_ROOT}} && veryl build

# Verilatorビルド（トレースなし）
build: veryl
  {{VERILATOR}} {{VERILATOR_FLAGS}} --top-module tb_adat_rx -Wno-DECLFILENAME -f {{PROJ_ROOT}}/adat_rx.f {{PROJ_ROOT}}/tests/tb_adat_rx.sv {{PROJ_ROOT}}/tests/adat_generator.sv sim_main.cpp

# Verilatorビルド（トレース付き）
build-trace: veryl
  {{VERILATOR}} {{VERILATOR_TRACE_FLAGS}} --top-module tb_adat_rx -Wno-DECLFILENAME -f {{PROJ_ROOT}}/adat_rx.f {{PROJ_ROOT}}/tests/tb_adat_rx.sv {{PROJ_ROOT}}/tests/adat_generator.sv sim_main.cpp

# 実行（トレースなし）
run: build
  ./{{OBJ_DIR}}/Vtb_adat_rx

# 実行（FSTトレース出力付き）
run-trace: build-trace
  ./{{OBJ_DIR}}/Vtb_adat_rx

# 波形表示 (Surfer)
wave: run-trace
  surfer "/Users/akiyuki/Documents/AkiyukiProjects/adat_rx/sim/verilator/adat_rx.fst"

# クリーンアップ
clean:
  cd {{PROJ_ROOT}} && veryl clean
  rm -rf {{OBJ_DIR}}
  rm -f {{PROJ_ROOT}}/target/*.sv

# タスク一覧
help:
  @just --list

# ユニットテスト一括（トレースなし）
unit-tests: veryl
  #!/usr/bin/env bash
  set -euo pipefail
  for mod in edge_detector timing_tracker bit_decoder frame_parser output_interface; do
    case "$mod" in
      edge_detector)
        top=tb_edge_detector
        tb="{{PROJ_ROOT}}/tests/tb_edge_detector.sv"
        rtl="{{PROJ_ROOT}}/target/edge_detector.sv"
        main=sim_main_edge.cpp
        ;;
      timing_tracker)
        top=tb_timing_tracker
        tb="{{PROJ_ROOT}}/tests/tb_timing_tracker.sv"
        rtl="{{PROJ_ROOT}}/target/timing_tracker.sv"
        main=sim_main_timing.cpp
        ;;
      bit_decoder)
        top=tb_bit_decoder
        tb="{{PROJ_ROOT}}/tests/tb_bit_decoder.sv"
        rtl="{{PROJ_ROOT}}/target/bit_decoder.sv"
        main=sim_main_bit.cpp
        ;;
      frame_parser)
        top=tb_frame_parser
        tb="{{PROJ_ROOT}}/tests/tb_frame_parser.sv"
        rtl="{{PROJ_ROOT}}/target/frame_parser.sv"
        main=sim_main_frame.cpp
        ;;
      output_interface)
        top=tb_output_interface
        tb="{{PROJ_ROOT}}/tests/tb_output_interface.sv"
        rtl="{{PROJ_ROOT}}/target/output_interface.sv"
        main=sim_main_output.cpp
        ;;
    esac
    {{VERILATOR}} {{VERILATOR_FLAGS}} --top-module "$top" -Wno-DECLFILENAME -f {{PROJ_ROOT}}/adat_rx.f "$tb" "$main"
    ./{{OBJ_DIR}}/V"$top"
  done

# ユニットテスト一括（トレースあり: FST）
unit-tests-trace: veryl
  #!/usr/bin/env bash
  set -euo pipefail
  for mod in edge_detector timing_tracker bit_decoder frame_parser output_interface; do
    case "$mod" in
      edge_detector)
        top=tb_edge_detector
        tb="{{PROJ_ROOT}}/tests/tb_edge_detector.sv"
        main=sim_main_edge.cpp
        ;;
      timing_tracker)
        top=tb_timing_tracker
        tb="{{PROJ_ROOT}}/tests/tb_timing_tracker.sv"
        main=sim_main_timing.cpp
        ;;
      bit_decoder)
        top=tb_bit_decoder
        tb="{{PROJ_ROOT}}/tests/tb_bit_decoder.sv"
        main=sim_main_bit.cpp
        ;;
      frame_parser)
        top=tb_frame_parser
        tb="{{PROJ_ROOT}}/tests/tb_frame_parser.sv"
        main=sim_main_frame.cpp
        ;;
      output_interface)
        top=tb_output_interface
        tb="{{PROJ_ROOT}}/tests/tb_output_interface.sv"
        main=sim_main_output.cpp
        ;;
    esac
    {{VERILATOR}} {{VERILATOR_TRACE_FLAGS}} --top-module "$top" -Wno-DECLFILENAME -f {{PROJ_ROOT}}/adat_rx.f "$tb" "$main"
    ./{{OBJ_DIR}}/V"$top"
  done
