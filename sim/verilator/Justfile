# ADAT Receiver Verilator justfile
#
# 使用方法:
#   just           - ビルドと実行(トレースなし)
#   just run       - ビルドと実行(トレースなし)
#   just run-trace - ビルドと実行（FSTトレース出力付き）
#   just wave      - 波形表示 (Surfer)
#   just clean     - クリーンアップ

PROJ_ROOT := "../.."
OBJ_DIR := "obj_dir"

VERILATOR := "verilator"
VERILATOR_FLAGS := "--cc --exe --build -j 0 -Wall -Wno-UNUSEDSIGNAL -Wno-UNUSEDPARAM -Wno-TIMESCALEMOD -Wno-WIDTHEXPAND -Wno-WIDTHTRUNC -Wno-PINCONNECTEMPTY --timing"
VERILATOR_TRACE_FLAGS := "--cc --exe --build -j 0 -Wall -Wno-UNUSEDSIGNAL -Wno-UNUSEDPARAM -Wno-TIMESCALEMOD -Wno-WIDTHEXPAND -Wno-WIDTHTRUNC -Wno-PINCONNECTEMPTY --timing --trace-fst"

default: test

# Verylフォーマット
fmt:
  cd {{PROJ_ROOT}} && veryl fmt

# Verylビルド
veryl: fmt
  cd {{PROJ_ROOT}} && veryl build

# Run Veryl integrated tests (primary route)
test:
  cd {{PROJ_ROOT}} && veryl test src/*.veryl

# Run Veryl integrated tests with waveform output (primary route)
test-wave:
  cd {{PROJ_ROOT}} && veryl test src/*.veryl --wave

# Legacy: Run all tests via veryl test (DEPRECATED - use 'just test' instead)
run: test

# Legacy: Verilatorビルド（トレースなし）
build: veryl
  {{VERILATOR}} {{VERILATOR_FLAGS}} --top-module tb_adat_rx -Wno-DECLFILENAME -f {{PROJ_ROOT}}/adat_rx.f {{PROJ_ROOT}}/tests/tb_adat_rx.sv {{PROJ_ROOT}}/tests/adat_generator.sv sim_main.cpp

# Legacy: Verilatorビルド（トレース付き）
build-trace: veryl
  {{VERILATOR}} {{VERILATOR_TRACE_FLAGS}} --top-module tb_adat_rx -Wno-DECLFILENAME -f {{PROJ_ROOT}}/adat_rx.f {{PROJ_ROOT}}/tests/tb_adat_rx.sv {{PROJ_ROOT}}/tests/adat_generator.sv sim_main.cpp

# Legacy: 実行（トレースなし）
# Direct Verilator execution (DEPRECATED - use 'just test' instead)
@_run-verilator: build
  ./{{OBJ_DIR}}/Vtb_adat_rx

# Legacy: 実行（FSTトレース出力付き）
# Direct Verilator execution with tracing (DEPRECATED - use 'just test-wave' instead)
@_run-verilator-trace: build-trace
  ./{{OBJ_DIR}}/Vtb_adat_rx

# 波形表示 (Surfer) - Legacy Verilator flow (DEPRECATED - use 'just test-wave' instead)
wave: _run-verilator-trace
  surfer "/Users/akiyuki/Documents/AkiyukiProjects/adat_rx/sim/verilator/adat_rx.fst"

# クリーンアップ
clean:
  cd {{PROJ_ROOT}} && veryl clean
  rm -rf {{OBJ_DIR}}
  rm -f {{PROJ_ROOT}}/target/*.sv

# タスク一覧
help:
  @just --list

# Legacy: ユニットテスト一括（トレースなし）
# DEPRECATED - use 'just test' instead
unit-tests: test

# Legacy: ユニットテスト一括（トレースあり: FST）
# DEPRECATED - use 'just test-wave' instead
unit-tests-trace: test-wave

